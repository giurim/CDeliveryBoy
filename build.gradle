buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'info.solidsoft.gradle:cdeliveryboy:0.3.0'
    }
}

apply plugin: 'groovy'
apply plugin: 'info.solidsoft.cdeliveryboy'

cDeliveryBoy {
    autoPromote = false
}

nexusStaging {
    packageGroup = "info.solidsoft"
}

scmVersion {
    hooks {
        pre 'fileUpdate', [file: 'README.md', pattern: {v, p -> /"info\.solidsoft\.gradle:cdeliveryboy:.*"/},
                           replacement: {v, p -> "\"info.solidsoft.gradle:cdeliveryboy:$v\""}]
    }
}

project.group = "info.solidsoft.gradle"
project.version = scmVersion.version

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    compile 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    compile 'pl.allegro.tech.build:axion-release-plugin:1.5.0'
    compile 'io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.8.0'
    
    testCompile('org.spockframework:spock-core:1.1-groovy-2.4-rc-3') {
        //groovy 2.4.x is already provided by Gradle itself
        exclude group: 'org.codehaus.groovy', module: 'groovy-all'
    }
    testCompile 'net.bytebuddy:byte-buddy:1.4.33' //for mocking in Spock - 1.5.x cannot be used due to: https://github.com/spockframework/spock/issues/678
    testCompile 'org.objenesis:objenesis:2.4'   //for mocking in Spock
    testCompile 'info.solidsoft.spock:spock-global-unroll:0.5.1'
}

def repoSlug = 'szpak/cdeliveryboy'
modifyPom {
    project {
        name 'cdeliveryboy'
        description 'CDeliveryBoy - Continuous Delivery for FOSS projects'
        url "https://github.com/$repoSlug"
        inceptionYear '2016'

        scm {
            url "https://github.com/$repoSlug"
            connection "scm:https://github.com/$repoSlug"
            developerConnection "scm:git://github.com/$repoSlug"
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'szpak'
                name 'Marcin ZajÄ…czkowski'
                email 'mszpak ATT wp DOTT pl'
                roles {
                    role 'developer'
                    role 'despot'
                }
            }
        }
    }
}

uploadArchives.dependsOn { check }
